plugins {
    id 'java'
    id 'java-library'
    id 'maven-publish'
}

group 'de.noisruker'
version '1.0.3'

java {
    withJavadocJar()
    withSourcesJar()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'logger'
            from components.java
            pom {
                name = 'Logger'
                description = 'A basic logging libary'
                url = 'https://github.com/juhu1705/Logger'
                licenses {
                    license {
                        name = 'GNU General Public License, Version 3.0'
                        url = 'https://www.gnu.org/licenses/gpl-3.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'juhu1705'
                        name = 'Fabius Mettner'
                        email = 'fabius1705@live.de'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/juhu1705/Logger.git'
                    developerConnection = 'scm:git:ssh://github.com/juhu1705/Logger.git'
                    url = 'http://github.com/juhu1705/Logger'
                }
            }
        }
    }

    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/juhu1705/logger")
            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("USERNAME")
                password = project.findProperty("gpr.key") ?: System.getenv("TOKEN")
            }
        }
    }
}

repositories {
    mavenCentral()
    maven {
        url = uri("https://maven.pkg.github.com/juhu1705/eventmanager")
        credentials {
            username = project.findProperty("gpr.user") ?: System.getenv("USERNAME")
            password = project.findProperty("gpr.key") ?: System.getenv("TOKEN")
        }
    }
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.2'
    implementation 'de.noisruker:event-manager:1.1.0'
}

test {
    useJUnitPlatform()
}

javadoc {
    if(JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}